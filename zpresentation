# zpresentation - zsh plugin for presentation
#
# Copyright (c) 2014 Hideaki Miyake
# Licensed under the MIT License (MIT)
#
# Author : Hideaki Miyake (https://github.com/mollifier)
# URL : https://github.com/mollifier/zpresentation
#

typeset -r SCRIPT_NAME="zpresentation"

function _zpresentation_print_usage()
{
  cat << EOF
Usage: $SCRIPT_NAME [PATH]

-h           display this help and exit
EOF
}

function _zpresentation_print_error()
{
  echo "${SCRIPT_NAME}: $@" 1>&2
  echo "Try \`-h' option for more information." 1>&2
}

function _zpresentation_escape_regex()
{
  echo -nE "$1" | sed -e 's/[][^\\.*$(){}+?|-]/\\&/g'
}

# すべてのヘッダ行を表示する
function _zpresentation_display_all_markdown_header()
{
  local file="$1"

  # remove header symbol (e.g. ## )
  grep -E "^#+[ \t]" -- "$file" \
    | sed 's/^#\{1,\}[ \t]\{1,\}//'
}

# 指定されたIDに対応するヘッダ行を表示する
function _zpresentation_display_markdown_header()
{
  local file="$1"
  # header_id is number (1, 2, 3, ...)
  local header_id="$2"

  _zpresentation_display_all_markdown_header "$file" | sed -n "${header_id}p"
}

# すべてのヘッダ行の行番号を表示する
function _zpresentation_display_markdown_all_header_line_number()
{
  local file="$1"
  grep -nE "^#+[ \t]" -- "$file" \
    | sed 's/:.*$//'
}

# 指定されたIDに対応するヘッダの行番号を表示する
function _zpresentation_display_markdown_header_line_number()
{
  local file="$1"
  # header_id is number (1, 2, 3, ...)
  local header_id="$2"

  _zpresentation_display_markdown_all_header_line_number "$file" | sed -n "${header_id}p"
}

# すべての段落を表示する
function _zpresentation_display_markdown_all_paragraph()
{
  local file="$1"
  local -a header_line_numbers
  header_line_numbers=( ${(@f)"$(_zpresentation_display_markdown_all_header_line_number "$file")"} )

  # TODO : ファイル全体の行数を取得して設定する
  header_line_numbers=($header_line_numbers 99999)

  local i
  local line_number next_line_number
  for ((i=1; i < $#header_line_numbers; i++)) do
    line_number=$header_line_numbers[$i]
    next_line_number=$header_line_numbers[$(( $i + 1 ))]

    # TODO : 空の段落を取り除く
    cat -- "$file" \
      | sed -n "$((${line_number} + 1 )),$(( ${next_line_number} - 1 ))p" \
      | tr -d '\n'
    # insert line feeds
    echo ""
  done

}

# 指定した見出しから次の見出しまでを出力する
# 次の見出し(同じレベルとは限らない)までを1ページ分のスライドとみなす
function _zpresentation_display_markdown_paragraph()
{
  local file="$1"
  local paragraph_id="$2"
  local next_paragraph_id="$(( $paragraph_id + 1 ))"

  local header_line_number="$(_zpresentation_display_markdown_header_line_number "$input_file" "$paragraph_id")"
  local next_header_line_number="$(_zpresentation_display_markdown_header_line_number "$input_file" "$next_paragraph_id")"

  cat -- "$file" | sed -n "$(( header_line_number + 1 )),$(( $next_header_line_number - 1 ))p"
}

function _zpresentation_main()
{
  # header, paragraph, ...
  local mode
  local header_id
  local paragraph_id
  local input_file=""

  local option OPTARG OPTIND
  while getopts ':f:e:p:EPh' option; do
    case $option in
      f)
        input_file="$OPTARG"
        ;;
      e)
        mode="header"
        header_id="$OPTARG"
        ;;
      p)
        mode="paragraph"
        paragraph_id="$OPTARG"
        ;;
      E)
        mode="all_header"
        ;;
      P)
        mode="all_paragraph"
        ;;
      h)
        _zpresentation_print_usage
        return 0
        ;;
      :)
        _zpresentation_print_error "option requires an argument -- $OPTARG"
        return 1
        ;;
      *)
        _zpresentation_print_error "invalid option -- $OPTARG"
        return 1
        ;;
    esac
  done
  shift $(expr $OPTIND - 1)

  if [[ -z "$input_file" ]]; then
    input_file="$1"
  fi

  case "$mode" in
    header)
      _zpresentation_display_markdown_header "$input_file" "$header_id"
      #_zpresentation_display_markdown_header_line_number "$input_file" "$header_id"
      ;;
    paragraph)
      _zpresentation_display_markdown_paragraph "$input_file" "$paragraph_id"
      ;;
    all_header)
      _zpresentation_display_all_markdown_header "$input_file"
      ;;
    all_paragraph)
      _zpresentation_display_markdown_all_paragraph "$input_file"
      ;;
  esac


}

#set -o xtrace
# treat unset parameters as an error
set -o no_unset

_zpresentation_main "$@"

set +o no_unset

#set +o xtrace

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
